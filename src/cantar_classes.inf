

!!==============================================================================
!!
!!	CANTAR DE LAS PEÑAS
!!	Definición de clases y atributos
!!
!!==============================================================================
!!
!!	Archivo:		cantar_classes.inf
!!	Autor(es):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Español)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		Máquina-Z/Glulx
!!	Versión:		1.0
!!	Fecha:			2019/01/XX
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de la ficción interactiva
!!	CANTAR DE LAS PEÑAS.
!!
!!	CANTAR DE LAS PEÑAS es software libre: usted puede redistribuirlo
!!	y/o modificarlo bajo los términos de la Licencia Pública General
!!	GNU publicada por la Fundación para el Software Libre, ya sea la
!!	versión 3 de la Licencia, o (a su elección) cualquier versión
!!	posterior.
!!
!!	CANTAR DE LAS PEÑAS se distribuye con la esperanza de que sea
!!	útil, pero SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita
!!	MERCANTIL o de APTITUD PARA UN PROPÓSITO DETERMINADO. Consulte los
!!	detalles de la Licencia Pública General GNU para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública General GNU
!!	junto a CANTAR DE LAS PEÑAS. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2019, J. Francisco Martín
!!
!!------------------------------------------------------------------------------
!!
!!	TABLA DE CONTENIDOS
!!
!!	<>	Receptacle
!!	<>	Room
!!	<>	Room:Indoors
!!	<>	Room:Outdoors
!!	<>	SmartEnterable
!!	<>	Rope
!!	<>	Thing
!!	<>	Thing:AdjacentRoom
!!	<>	Thing:Atrezzo
!!	<>	Thing:HeavyItem
!!	<>	Thing:Item
!!	<>	Thing:Item:Clothes
!!	<>	Thing:NPC
!!	<>	Thing:PC
!!	<>	Weapon
!!	<>	Weapon:Dagger
!!	<>	Weapon:Sword
!!
!!------------------------------------------------------------------------------
!!
!!	NOTA SOBRE LAS PROPIEDADES DE CLASE
!!
!!	Hay rutinas como 'before' o 'react_before' que son propiedades no aditivas,
!!	es decir, si se crea una clase que defina un comportamiento específico para
!!	una de estas propiedades, y a continuación se crea un objeto como instancia
!!	de esta clase de forma que también él defina su propia versión de alguna de
!!	estas propiedades no aditivas, las rutinas del objeto "sobreescribirán"
!!	completamente aquellas de la clase. Dicho de otra manera, si el objeto
!!	define las suyas propias, las rutinas 'before' y 'react_before' de la clase
!!	de la que es instancia NO SERÁN INVOCADAS en ningún momento. Sin embargo,
!!	se puede hacer una llamada explícita a la rutina de la clase desde la
!!	rutina del objeto. A continuación se especifica cómo un objeto 'objectName'
!!	invocaría a la rutina 'react_before' de una superclase 'className' de su
!!	jerarquía de clases:
!!
!!		objectName.className::react_before();
!!
!!------------------------------------------------------------------------------


!!------------------------------------------------------------------------------
!! Receptacle
!!------------------------------------------------------------------------------
!! 'Receptacle' define contenedores o soportes con límites de capacidad (en
!! cantidad, tamaño, volumen y peso). Al tratar de introducir cualquier objeto
!! en un 'Receptacle' se ejecutan automáticamente las comprobaciones oportunas
!! para determinar si el contenedor tiene o no capacidad suficiente para
!! albergar el nuevo objeto.
!!
!!	@dependency SpanishLM.h
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	Receptacle
 with	before [;
			Receive:
				switch (Utils.check_object_capacity(self, noun)) {
					!! Superado el límite en el número de objetos:
					-1:
						print "En ", (the) self, " ya no ";
						switch (Utils.get_grammatical_inflection()) {
							FIRST_PERSON_PRESENT,
							SECOND_PERSON_PRESENT,
							THIRD_PERSON_PRESENT:
								print "cabe";
							FIRST_PERSON_PAST,
							SECOND_PERSON_PAST,
							THIRD_PERSON_PAST:
								print "cabía";
							FIRST_PERSON_FUTURE,
							SECOND_PERSON_FUTURE,
							THIRD_PERSON_FUTURE:
								print "cabrá";
						}
						" nada más.";
					!! Superado el límite de tamaño:
					-2:
						print (The) noun, " ";
						switch (Utils.get_grammatical_inflection()) {
							FIRST_PERSON_PRESENT,
							SECOND_PERSON_PRESENT,
							THIRD_PERSON_PRESENT:
								print (es) noun;
							FIRST_PERSON_PAST,
							SECOND_PERSON_PAST,
							THIRD_PERSON_PAST:
								print "era", (n) noun;
							FIRST_PERSON_FUTURE,
							SECOND_PERSON_FUTURE,
							THIRD_PERSON_FUTURE:
								print "será", (n) noun;
						}
						print " demasiado grande", (s) noun, " para";
						if (self has supporter) {
							print " colocarl", (o) noun, " sobre";
						} else if (self has container) {
							print " meterl", (o) noun, " en";
						}
						!! XXX - El mensaje es adecuado aunque el contenedor
						!! esté mal definido y no tenga las propiedades
						!! 'supporter' o 'container'.
						" ", (the) self, ".";
					!! Superado el límite de volumen:
					-3:
						print (The) noun, " ";
						switch (Utils.get_grammatical_inflection()) {
							FIRST_PERSON_PRESENT,
							SECOND_PERSON_PRESENT,
							THIRD_PERSON_PRESENT:
								print (es) noun;
							FIRST_PERSON_PAST,
							SECOND_PERSON_PAST,
							THIRD_PERSON_PAST:
								print "era", (n) noun;
							FIRST_PERSON_FUTURE,
							SECOND_PERSON_FUTURE,
							THIRD_PERSON_FUTURE:
								print "será", (n) noun;
						}
						print " demasiado voluminos", (o) noun, " para";
						if (self has supporter) {
							print " colocarl", (o) noun, " sobre";
						} else if (self has container) {
							print " meterl", (o) noun, " en";
						}
						" ", (the) self, ".";
					!! Superado el límite de volumen agregado:
					-4:
						print "No ";
						switch (Utils.get_grammatical_inflection()) {
							FIRST_PERSON_PRESENT,
							SECOND_PERSON_PRESENT,
							THIRD_PERSON_PRESENT:
								print "hay";
							FIRST_PERSON_PAST,
							SECOND_PERSON_PAST,
							THIRD_PERSON_PAST:
								print "había";
							FIRST_PERSON_FUTURE,
							SECOND_PERSON_FUTURE,
							THIRD_PERSON_FUTURE:
								print "habrá";
						}
						print " hueco suficiente en ", (the) self, " para";
						if (self has supporter) {
							print " colocar";
						} else if (self has container) {
							print " meter";
						}
						" ", (the) noun, ".";
					!! Superado el límite de peso:
					-5:
						print (The) noun, " ";
						switch (Utils.get_grammatical_inflection()) {
							FIRST_PERSON_PRESENT,
							SECOND_PERSON_PRESENT,
							THIRD_PERSON_PRESENT:
								print (es) noun;
							FIRST_PERSON_PAST,
							SECOND_PERSON_PAST,
							THIRD_PERSON_PAST:
								print "era", (n) noun;
							FIRST_PERSON_FUTURE,
							SECOND_PERSON_FUTURE,
							THIRD_PERSON_FUTURE:
								print "será", (n) noun;
						}
						print " demasiado pesad", (o) noun, " para";
						if (self has supporter) {
							print " colocarl", (o) noun, " sobre";
						} else if (self has container) {
							print " meterl", (o) noun, " en";
						}
						" ", (the) self, ".";
					!! Superado el límite de peso agregado:
					-6:
						print (The) self, " no ";
						switch (Utils.get_grammatical_inflection()) {
							FIRST_PERSON_PRESENT,
							SECOND_PERSON_PRESENT,
							THIRD_PERSON_PRESENT:
								print "puede", (n) self;
							FIRST_PERSON_PAST,
							SECOND_PERSON_PAST,
							THIRD_PERSON_PAST:
								print "podía", (n) self;
							FIRST_PERSON_FUTURE,
							SECOND_PERSON_FUTURE,
							THIRD_PERSON_FUTURE:
								print "podrá", (n) self;
						}
						" soportar tanto peso.";
				}
		],
;

!!------------------------------------------------------------------------------
!! Room
!!------------------------------------------------------------------------------
!! Representa un objeto localidad dentro del modelo de mundo.
!!
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	Room
 with	description [;
			if (action == ##Look || self hasnt visited) {
				self.long_description();
			} else self.short_description();
			!! Espacio para elementos comunes a las dos descripciones.
			new_line;
			return true;
		],
		long_description "EN CONSTRUCCIÓN.",
		!! Por defecto, la descripción corta es la misma descripción larga:
		short_description [; return self.long_description(); ],
		!! Permite que se inhiba el listado automático de objetos al imprimir
		!! la descripción de la localidad. (XXX Para ello se debe modificar el
		!! comportamiento de la acción ##Look en el archivo de mensajes de la
		!! librería):
		inhibit_object_list false,
		!! Código de estado. Ideado como reemplazo a la propiedad 'general',
		!! por añadir mayor versatilidad al utilizar enteros en lugar de un
		!! valor booleano:
		state 0,
 has	light;

!!------------------------------------------------------------------------------
!! Room:Indoors
!!------------------------------------------------------------------------------
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	Indoors
 class	Room
;

!!------------------------------------------------------------------------------
!! Room:Outdoors
!!------------------------------------------------------------------------------
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	Outdoors
 class	Room
;

!!------------------------------------------------------------------------------
!! SmartEnterable
!!------------------------------------------------------------------------------
!! Representa un objeto al que el PC puede entrar o subirse encima, con una
!! serie de comportamientos automáticos que afectan al PC si trata de realizar
!! acciones desde dentro de un objeto de esta clase.
!!
!!	@dependency AdjacentRoom
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	SmartEnterable
 with	react_before [;
			if (parent(player) ~= self) return false;
			!! GROUP #2: Usually work, given the right circumstances.
			Close,
			Open:
				if (noun hasnt openable) return false;
				if (self.out_of_reach(noun)) {
					<Exit self>;
					new_line;
				}
				return false;
			Disrobe,
			Wear:
				if (noun hasnt clothing) return false;
				<Exit self>;
				new_line;
				return false;
			Drop:
				if (self has container) return false;
				if ((noun == player) || (parent(noun) ~= player)) return false;
				if (self.drop_on_object == 0) move noun to self;
				else move noun to self.drop_on_object;
				if (keep_silent == 0) print "Dejad", (o) noun, ".^";
				return true;
			Eat:
				if (noun hasnt edible) return false;
				if (self.out_of_reach(noun)) {
					<Exit self>;
					new_line;
				}
				return false;
			Empty,
			Remove,
			Search,
			Take:
				if (self.out_of_reach(noun)) {
					<Exit self>;
					new_line;
				}
				return false;
			EmptyT: !! TODO - Comprobar funcionamiento
				if (ObjectIsUntouchable(noun, 1)) return false;
				if (self.out_of_reach(second))
					{ <Exit self>; new_line; }
				return false;
			!! Enter:
			!! Examine:
			!! Exit:
			!! GetOff:
			!! Give:
			Go:
				if (noun ofclass CompassDirection
					|| noun ofclass AdjacentRoom) {
					<Exit self>;
					new_line;
				}
				return false;
			!! GoIn:
			Insert,
			PutOn:
				if ((second == d_obj) || (player in second)) {
					<<Drop noun>>;
				}
				if (self.out_of_reach(second)) {
					<Exit self>;
					new_line;
				}
				return false;
			!! Inv:
			!! InvTall:
			!! InvWide:
			Lock,
			Unlock:
				if (noun hasnt lockable) return false;
				if (self.out_of_reach(noun)) {
					<Exit self>;
					new_line;
				}
				return false;
			!! Look:
			!! Open:		Ver ##Close
			!! Order:
			!! PutOn:		Ver ##Insert
			!! Remove:		Ver ##Empty
			!! Search:		Ver ##Empty
			!! Show:
			SwitchOff,
			SwitchOn:
				if (noun hasnt switchable) return false;
				if (self.out_of_reach(noun)) {
					<Exit self>;
					new_line;
				}
				return false;
			!! SwitchOn:	Ver ##SwitchOff
			!! Take:		Ver ##Empty
			!! Transfer:
			!! Unlock:		Ver ##Lock
			!! VagueGo:
			!! Wear:		Ver ##Disrobe
			!! GROUP #3: Output a message and stop at the "before" stage.
			!! Answer:
			!! Ask:
			!! AskFor:
			Attack,
			Blow,
			LookUnder,
			Smell,
			Squeeze,
			Taste,
			Touch:
				if (self.out_of_reach(noun)) {
					<Exit self>;
					new_line;
				}
				return false;
			!! Blow:		Ver ##Attack
			!! Burn:
			!! Buy:
			Climb:
				if (noun hasnt enterable) return false;
				if (self.out_of_reach(noun)) {
					<Exit self>;
					new_line;
				}
				return false;
			!! Consult:
			!! Cut:
			!! Dig:
			!! Drink:
			!! Fill:
			Jump:
				if (noun has supporter) <<Exit self>>;
				return false;
			!! JumpOver:
			!! Kiss:
			!! LetGo:
			!! Listen:
			!! LookUnder:	Ver ##Attack
			!! Mild:
			!! No:
			!! Pray:
			Pull,
			Push,
			Turn:
				!! TODO - Comprobar mensaje al empujar objetos scenery o static
				if (noun has scenery or static) return false;
				if (self.out_of_reach(noun)) {
					<Exit self>;
					new_line;
				}
				return false;
			!! Push:		Ver ##Pull
			!! PushDir:
			!! Receive:
			!! Rub:
			!! Set:
			!! SetTo:
			!! Sing:
			!! Sleep:
			!! Smell:		Ver ##Attack
			!! Sorry:
			!! Squeeze:	Ver ##Attack
			!! Strong:
			!! Swim:
			Swing:
				if (noun has supporter) <<Swing self>>;
				return false;
			!! Taste:		Ver ##Attack
			!! Tell:
			!! Think:
			!! ThrowAt:
			!! Tie:
			!! Touch:		Ver ##Attack
			!! Turn:		Ver ##Pull
			!! Wait:
			!! Wake:
			!! WakeOther:
			!! Wave:
			!! WaveHands:
			!! Yes:
			!! CUSTOM ACTIONS
			!! Dance:
			GoDown:
				if (self has supporter) <<Exit self>>;
				return false;
			!! GoUp:
			!! KnockOn:
			!! Masturbate:
			!! Sit:
			!! SleepWith:
			StandUp:
				if ((self has supporter) && (noun == nothing or self)) {
					<<Exit self>>;
				}
			TalkTo:
				if (noun hasnt animate) return false;
				if (self.out_of_reach(noun)) {
					<Exit self>;
					new_line;
				}
				return false;
			!! TalkTo:
			!! Untie:
			!! Use:
			!! Write:
			!! Xyzzy:
		],
		!! Objetos al alcance desde el SmartEnterable:
		reach_objects 0,
		!! Objeto destino de la acción ##Drop estando en el SmartEnterable:
		drop_on_object 0,
 private
		!! Indica si el objeto pasado como parámetro se encuentra fuera de
		!! alcance desde el SmartEnterable. Retorna verdadero si el objeto está
		!! fuera de alcance, falso en caso contrario. [Código adaptado de la
		!! librería OutOfRch.h v2.00, de Marnie Parker <doeadeer3@aol.com>].
		out_of_reach [ obj
			top x y i;
			!! Se comprueba si el objeto se encuentra fuera de la localidad o
			!! en el interior de un contenedor cerrado:
			if (ObjectIsUntouchable(obj, true)) return false;
			!! Se comrpueba si el objeto se encuentra en el jugador o en el
			!! propio SmartEnterable:
			if (IndirectlyContains(player, obj)) return false;
			if (IndirectlyContains(self, obj)) return false;
			!! Se comprueba si el objeto es o se encuentra contenido en alguno
			!! de los reach_objects definidas:
			top = Utils.get_top_holder(obj);
			if (metaclass(self.&reach_objects-->0) == Routine) {
				x = self.reach_objects();
				if (x == top) return false;
			}
			if (metaclass(self.&reach_objects-->0) == Object or Class) {
				y = self.#reach_objects;
				for (i = 0 : i < (y/2) : i++) {
					x = self.&reach_objects-->i;
					if (x == top) return false;
				}
			}
			!! El objeto se encuentra fuera del alcance:
			return true;
		],
 has	enterable;

!!------------------------------------------------------------------------------
!! Thing
!!------------------------------------------------------------------------------
!! Representa un objeto dentro del modelo del mundo, con un tamaño, volumen y
!! peso específicos, así como diferentes valores de capacidad asociados a su
!! potencial como contenedor para otros objetos. Está ideada como una clase
!! abstracta sobre la que definir el resto de subclases más concretas de
!! objetos con los que modelar el mundo del relato.
!!
!!	@dependency SpanishLM.h
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	Thing
 with	capacity_size	-1,	! Capacidad total: en cuanto a tamaño
		capacity_volume	-1,	! Capacidad total: en cuanto a volumen
		capacity_weight	-1,	! Capacidad total: en cuanto a peso
		size	0,			! Tamaño
		state	0,			! Estado
		volume	0,			! Volumen
		weight	0,			! Peso
		description [;
			switch (Utils.get_grammatical_inflection()) {
				FIRST_PERSON_PRESENT:
					if (IsPluralNoun(player)) print "No vemos";
					else print "No veo";
				SECOND_PERSON_PRESENT:
					if (IsPluralNoun(player)) print "No veis";
					else print "No ves";
				THIRD_PERSON_PRESENT:
					if (IsPluralNoun(player)) print "No ven";
					else print "No ve";
				FIRST_PERSON_PAST:
					if (IsPluralNoun(player)) print "No vimos";
					else print "No vi";
				SECOND_PERSON_PAST:
					if (IsPluralNoun(player)) print "No visteis";
					else print "No viste";
				THIRD_PERSON_PAST:
					if (IsPluralNoun(player)) print "No vieron";
					else print "No vio";
				FIRST_PERSON_FUTURE:
					if (IsPluralNoun(player)) print "No veremos";
					else print "No veré";
				SECOND_PERSON_FUTURE:
					if (IsPluralNoun(player)) print "No veréis";
					else print "No verás";
				THIRD_PERSON_FUTURE:
					if (IsPluralNoun(player)) print "No verán";
					else print "No verá";
			}
			" nada especial en ", (the) self, ".";
		],
;

!!------------------------------------------------------------------------------
!! Thing:AdjacentRoom
!!------------------------------------------------------------------------------
!! Representa un objeto dentro del modelo de mundo hacia el que el PC se puede
!! desplazar.
!!
!!	@dependency arrays.h - FIXME para imprimir la aclaración
!!	@dependency textStyles.h
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	AdjacentRoom
 class	Thing
 with	short_name [
			obj;
			if (self.direction_obj ~= 0 && self.compass_reference) {
				if (metaclass(self.direction_obj) == Routine) {
					obj = self.direction_obj();
				}
				if (metaclass(self.direction_obj) == Object) {
					obj = self.direction_obj;
				}
				print (object) self, " ", (al) obj;
				return true;
			}
		],
		before [
			previous_style obj;
			DefaultAction:
				previous_style = Utils.set_text_style(TEXT_STYLE_PARSER);
				print (string) TEXT_STYLE_PARSER_PREFIX;
				print "ve ", (al_) self, "", (object) self;
				print (string) TEXT_STYLE_PARSER_SUFIX;
				Utils.set_text_style(previous_style);
				new_line;
				<<Enter self>>;
			Enter,
			Go:
				if (self.direction_obj ~= 0) {
					if (metaclass(self.direction_obj) == Routine) {
						obj = self.direction_obj();
					}
					if (metaclass(self.direction_obj) == Object) {
						obj = self.direction_obj;
					}
					<<Go obj>>;
				}
			Examine,
			Look:
				return L__M(##Look, 7, self);
		],
		compass_reference false,
		direction_obj 0,
 has	open scenery;

!!------------------------------------------------------------------------------
!! Thing:Atrezzo
!!------------------------------------------------------------------------------
!! Representa un objeto decorado dentro del modelo de mundo. Se le aplica la
!! propiedad 'scenery' definida por la librería Inform.
!!
!!	@dependency SpanishLM.h
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	Atrezzo
 class	Thing
 with	before [;
			DefaultAction,
			Examine,
			Search:
				return false;
			default:
				switch (Utils.get_grammatical_inflection()) {
					FIRST_PERSON_PRESENT:
						if (IsPluralNoun(player))
							print "No creemos que necesitemos preocuparnos";
						else print "No creo que necesite preocuparme";
					SECOND_PERSON_PRESENT:
						print "No es necesario que ";
						if (IsPluralNoun(player)) print "os preocupéis";
						else print "te preocupes";
					THIRD_PERSON_PRESENT:
						print "No es necesario que ";
						if (IsPluralNoun(player)) print "se preocupen";
						else print "se preocupe";
					FIRST_PERSON_PAST:
						if (IsPluralNoun(player))
							print "No creíamos que necesitásemos preocuparnos";
						else print "No creía que necesitase preocuparme";
					SECOND_PERSON_PAST:
						print "No era necesario que ";
						if (IsPluralNoun(player)) print "os preocupaseis";
						else print "te preocupases";
					THIRD_PERSON_PAST:
						print "No era necesario que ";
						if (IsPluralNoun(player)) print "se preocupasen";
						else print "se preocupase";
					FIRST_PERSON_FUTURE:
						print "Es difícil que";
						if (IsPluralNoun(player))
							print "vayamos a necesitar preocuparnos";
						else print "vaya a necesitar preocuparme";
					SECOND_PERSON_FUTURE:
						print "No será necesario que ";
						if (IsPluralNoun(player)) print "os preocupéis";
						else print "te preocupes";
					THIRD_PERSON_FUTURE:
						print "No será necesario que ";
						if (IsPluralNoun(player)) print "se preocupen";
						else print "se preocupe";
				}
				print " por ";
				Utils.spanish_nominative_pronoun(self);
				".";
		],
 has	scenery;

!!------------------------------------------------------------------------------
!! Thing:HeavyItem
!!------------------------------------------------------------------------------
!! Representa un objeto pesado dentro del modelo de mundo. Se le aplica la
!! propiedad 'static' definida por la librería Inform.
!!
!!	@dependency SpanishLM.h
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	HeavyItem
 class	Thing
 with	before [;
			Pull,
			Push,
			PushDir,
			Take,
			Turn:
				switch (Utils.get_grammatical_inflection()) {
					FIRST_PERSON_PRESENT,
					SECOND_PERSON_PRESENT,
					THIRD_PERSON_PRESENT:
						print (The) self, " ", (es) self;
					FIRST_PERSON_PAST,
					SECOND_PERSON_PAST,
					THIRD_PERSON_PAST:
						print (The) self, " era", (n) self;
					FIRST_PERSON_FUTURE,
					SECOND_PERSON_FUTURE,
					THIRD_PERSON_FUTURE:
						print (The) self, " será", (n) self;
				} " demasiado pesad", (o) self, " para andar moviéndol",
				(o) self, ".";
		],
 has	static;

!!------------------------------------------------------------------------------
!! Thing:Item
!!------------------------------------------------------------------------------
!! Representa un objeto simple dentro del modelo de mundo sobre el que
!! inicialmente no se aplican restricciones de ningún tipo.
!!
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	Item
 class	Thing
;

!!------------------------------------------------------------------------------
!! Thing:Item:Clothes
!!------------------------------------------------------------------------------
!! Objetos vestibles. Implementa un mecanismo de capas de modo que no es
!! posible vestir ni desvestir cualquier prenda de una capa mientras el PC
!! lleve puestas prendas de una capa superior.
!!
!!	@dependency SpanishLM.h
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	Clothes
 class	Item
 with	before [
			obj i temp layer_a layer_b;
			Disrobe,
			Wear:
				!! Calcula la capa de referencia del objeto:
				for (i = 0 : i < (self.#layers)/WORDSIZE : i++) {
					temp = self.&layers-->i;
					if (temp > layer_a) layer_a = temp;
				}
				!! Comprueba las capas del resto de elementos vestidos por PJ:
				objectloop(obj in player && obj ofclass Clothes
					&& obj has worn && obj ~= self) {
					for (i = 0 : i < (obj.#layers)/WORDSIZE : i++) {
						temp = obj.&layers-->i;
						if (i == 0) layer_b = temp;
						else if (temp < layer_b) layer_b = temp;
					}
					if (layer_a < layer_b) {
						switch (Utils.get_grammatical_inflection()) {
							FIRST_PERSON_PAST:
								if (IsPluralNoun(player))
									print "Teníamos que quitarnos";
								else print "Tenías que quitarme";
							SECOND_PERSON_PAST:
								if (IsPluralNoun(player))
									print "Teníais que quitaros";
								else print "Tenías que quitarte";
							THIRD_PERSON_PAST:
								if (IsPluralNoun(player))
									print "Tenían que quitarse";
								else print "Tenía que quitarse";
							FIRST_PERSON_PRESENT:
								if (IsPluralNoun(player))
									print "Tenemos que quitarnos";
								else print "Tengo que quitarme";
							SECOND_PERSON_PRESENT:
								if (IsPluralNoun(player))
									print "Tenéis que quitaros";
								else print "Tienes que quitarte";
							THIRD_PERSON_PRESENT:
								if (IsPluralNoun(player))
									print "Tienen que quitarse";
								else print "Tiene que quitarse";
							FIRST_PERSON_FUTURE:
								if (IsPluralNoun(player))
									print "Tendremos que quitarnos";
								else print "Tendré que quitarme";
							SECOND_PERSON_FUTURE:
								if (IsPluralNoun(player))
									print "Tendréis que quitaros";
								else print "Tendrás que quitarte";
							THIRD_PERSON_FUTURE:
								if (IsPluralNoun(player))
									print "Tendrán que quitarse";
								else print "Tendrá que quitarse";
						}
						" ", (the) obj, " primero.";
					}
				}
		],
		layers 0,
 has	clothing;

!!------------------------------------------------------------------------------
!! Rope
!!------------------------------------------------------------------------------
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	Rope
;

!!------------------------------------------------------------------------------
!! Thing:NPC
!!------------------------------------------------------------------------------
!! Representa un personaje no controlado por el usuario.
!!
!!	@dependency arrays.h
!!	@dependency textStyles.h
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	NPC
 class	Thing,
 with	before [
			previous_style;
			DefaultAction:
				if (self has described) {
					previous_style = Utils.set_text_style(TEXT_STYLE_PARSER);
					print (string) TEXT_STYLE_PARSER_PREFIX;
					print "habla con ";
					PrintToBuffer(temp_array, INPUT_BUFFER_LEN, self);
					PrintCharacterArray(temp_array);
					print (string) TEXT_STYLE_PARSER_SUFIX;
					Utils.set_text_style(previous_style);
					new_line;
					<<TalkTo self>>;
				}
		],
 has	animate;

!!------------------------------------------------------------------------------
!! Thing:PC
!!------------------------------------------------------------------------------
!! Representa un personaje controlado por el usuario.
!!
!!	@dependency arrays.h
!!	@dependency textStyles.h
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	PC
 class	Thing,
 with	get_archetype [;
			return "";
		],
		get_experience [;
			return self.experience;
		],
		get_vitality [;
			return self.vitality;
		],
		modify_experience [ amount
			temp;
			!! Cálculo del nuevo valor de experiencia:
			temp = self.experience + amount;
			if (temp<0) temp = 0;
			!! Notificación:
			Utils.print_notification((amount>0), self, "experiencia",
				amount, temp);
			!! Efectos:
			!! TODO - Gestión de niveles
			return true;
		],
		modify_vitality [ amount
   			temp;
			!! Cálculo del nuevo valor de vitalidad:
			temp = self.vitality + amount;
			if (temp>100) temp = 100;
			if (temp<0) temp = 0;
			!! Notificación:
			Utils.print_notification((amount>0), self, "vitalidad",
				amount, temp);
			!! Efectos:
			self.vitality = temp;
			if (self.vitality == 0) {
				Utils.print_notification(false, self, "ha muerto");
			}
			return true;
		],
		image 0,
		specialization 0,
 private
		experience 0,
		vitality 100,
 has	animate;

!!------------------------------------------------------------------------------
!! Thing:Weapon
!!------------------------------------------------------------------------------
!! Representa un objeto que puede ser utilizado como arma.
!!
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	Weapon
 class	Thing,
 with	attack_value 0,
;

!!------------------------------------------------------------------------------
!! Thing:Weapon:Dagger
!!------------------------------------------------------------------------------
!!
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	Dagger
 class	Weapon,
 with	attack_value 5,
;

!!------------------------------------------------------------------------------
!! Thing:Weapon:ShortSword
!!------------------------------------------------------------------------------
!!
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	ShortSword
 class	Weapon,
 with	attack_value 10,
;

!!------------------------------------------------------------------------------
!! Thing:Weapon:LongSword
!!------------------------------------------------------------------------------
!!
!!	@version 1.0
!!------------------------------------------------------------------------------
Class	LongSword
 class	Weapon,
 with	attack_value 15,
;
